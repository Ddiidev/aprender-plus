;<kix2exe>
;<version>1.4.0.0</version>
;<settings>
;   <package>Aprender+.exe</package>
;   <icon>\aprender\img\icon.ico</icon>
;   <kix>Wkix32</kix>
;   <consolehide></consolehide>
;   <consolesize></consolesize>
;   <consoletitle></consoletitle>
;   <keepconsoleopen></keepconsoleopen>
;   <include></include>
;   <runasuser></runasuser>
;   <runaspassword></runaspassword>
;   <runasinteractive></runasinteractive>
;   <runasnoprofile></runasnoprofile>
;   <uaclevel>User</uaclevel>
;   <branding-name>Aprender+</branding-name>
;   <branding-desc>Sistema de gestão, com foco em empresas de cursos.</branding-desc>
;   <branding-company>André Luiz, Lucas Lima</branding-company>
;   <branding-version>1.0.0.1</branding-version>
;   <branding-vernum>1.0.0.11</branding-vernum>
;   <branding-prodver>1.0.0.1</branding-prodver>
;   <branding-copyright>0.1 Sistemas</branding-copyright>
;   <balloontips></balloontips>
;   <kixforms></kixforms>
;</settings>
;</kix2exe>

;;<CONFIGURANDO AMBIENTE>
	Break on
	$=SetOption(Explicit, on)
	; $=Redirectoutput("nul")
	function true() $true=1 endfunction
	function false() $false=0 endfunction
	function default() $default=1 endfunction
	include "Includes\KiXDB\Tokens.udf"
	include "Includes\KiXDB\hash.Udf"
	include "Includes\KiXDB\AlsArray.udf"
	include "Includes\KiXDB\AlsString.Udf"
	include "Includes\KiXDB\KiXDB.Udf"
	include "Includes\Formating.Udf"
	include "Includes\Flip_Currency.Udf"
	include "Includes\DateCalc.Udf"
;;</CONFIGURANDO AMBIENTE>
;;<DECLARANDO VARIÁVEIS>
	Global
		$system, $form, $main_img, $icon
	$system = CreateObject("Kixtart.System")
;;</DECLARANDO VARIÁVEIS>

;;<IMAGELIST>
	:_img
	$main_img					= $System.ImageList()
	$main_img.TransparentColor	= $System.Color(0,0,0)
	$main_img.ColorDepth		= 24
	$main_img.ImageSize			= 50, 50
	$=$main_img.images.AddStrip(@Curdir+"\img\Icon-Program\contrato.bmp", $System.Color(0,0,0), 512, 512)
	$=$main_img.images.AddStrip(@Curdir+"\img\Icon-Program\funcionario.bmp", $System.Color(0,0,0), 100, 100)
	$=$main_img.images.AddStrip(@Curdir+"\img\Icon-Program\pagamento.bmp", $System.Color(0,0,0), 256, 256)
 	$=$main_img.images.AddStrip(@Curdir+"\img\LOGO\logo.bmp", $System.Color(0,0,0), 256, 256)
 	$=$main_img.images.AddStrip(@Curdir+"\img\Icon-Program\pagamento-aluno.bmp", $System.Color(0,0,0), 96, 96)
	
	$icon					= $System.ImageList()
	$icon.TransparentColor	= $System.Color(0,0,0)
	$icon.ImageSize			= 25, 25
	$=$icon.images.AddStrip(@curdir+"\img\Icon-Program\ok.bmp", $System.Color(0,0,0), 209, 177)
	$=$icon.images.AddStrip(@curdir+"\img\Icon-Program\add.bmp", $System.Color(0,0,0), 206, 178)
	$=$icon.images.AddStrip(@curdir+"\img\Icon-Program\sub.bmp", $System.Color(0,0,0), 209, 177)
	$=$icon.images.AddStrip(@curdir+"\img\Icon-Program\edit.bmp", $System.Color(0,0,0), 209, 178)
	$=$icon.images.AddStrip(@curdir+"\img\Icon-Program\cancel.bmp", $System.Color(0,0,0), 208, 177)
	$=$icon.images.AddStrip(@curdir+"\img\Icon-Program\ver.bmp", $System.Color(0,0,0), 210, 177)
;;</IMAGELIST>

;;<CARREGANDO TABELAS>
	:LOAD_TBS
	tb_load(@Curdir+"\DataBase\Generetors.ktb", "Gen")
	tb_load(@Curdir+"\DataBase\usuario.ktb", "usuario")
	tb_load(@Curdir+"\DataBase\Empresa.ktb", "Empresa")
	tb_load(@Curdir+"\DataBase\Contrato.ktb", "Contrato")
	tb_load(@Curdir+"\DataBase\Base-Curso.ktb", "cursos")
	tb_load(@Curdir+"\DataBase\Funcionario.ktb", "funcionario")
	tb_load(@Curdir+"\DataBase\Dados-Comissao.ktb", "Dados-Comissao")
	tb_load(@Curdir+"\DataBase\Cadastro-Aluno.ktb", "Cadastro-Aluno")
	tb_load(@Curdir+"\DataBase\Perfil-Comissao.ktb", "Perfil-Comissao")
	tb_load(@Curdir+"\DataBase\pagamento-salario.ktb", "pagamento-salario")
	tb_load(@Curdir+"\DataBase\pagamento-comissao.ktb", "pagamento-comissao")
	tb_load(@Curdir+"\DataBase\pagamento-mensalidade-aluno.ktb", "pag-mensalidade-aluno")
;;</CARREGANDO TABELAS>

$form			= $system.form()
$form.BeginUpdate()
$form.Text		= "0.1 Sistemas - Aprender+ | "+tb_getData("empresa.NOME-FANTASIA", 1)
$form.icon		= @Curdir+"\img\icon.ico"
$form.size		= 900, 650
$form.center()

;;<CHAMANDO INTERFACE EXTERNAS>
	include "forms\form_main.KiX"
	include "forms\objHerança.KiX"
	include "forms\form_Painel.KiX"
	include "forms\pnlContrato.KiX"
	include "forms\pnlPagamento.KiX"
	include "forms\pnlFuncionario.KiX"
	include "forms\pnlPagamentoAluno.KiX"
;;</CHAMANDO INTERFACE EXTERNAS>

$form.EndUpdate()
$form.Activate()
$form.show()
while $form.visible()
	$=Execute($system.Application.DoEvents())
loop

function ViewText($sys, $frm, $title, $text)
	dim $ViewText, $tx, $
	$frm.Enabled		= 0
	$ViewText			= $sys.form($frm)
	$ViewText.Text		= $title
	$ViewText.Width		= 450
	$tx					= $ViewText.TextBox()
	$tx.BackColor		= "lightgray"
	$tx.ReadOnly		= 1
	$tx.MultiLine		= 1
	$tx.font			= $sys.Font("Consolas", 12, 1)
	$tx.ForeColor		= "Black"
	$tx.BorderStyle		= 0
	$tx.ScrollBars		= 3
	$tx.Dock			= 5
	$tx.text			= $text
	
	$ViewText.Center()
	$ViewText.Show()
	while $ViewText.Visible()
		$=Execute($ViewText.DoEvents())
	loop
	$frm.Enabled	= 1
endfunction

function NDiaToTexto($text, optional $completo)
	dim $i
	$completo  = iif($completo, " feira", "")
	$text = split($text, ",")
	for $i=0 to ubound($text)
		select
			case ($text[$i] = 1)
				$text[$i] = "Domingo"
			case ($text[$i] = 2)
				$text[$i] = "Segunda"+$completo
			case ($text[$i] = 3)
				$text[$i] = "Terça"+$completo
			case ($text[$i] = 4)
				$text[$i] = "Quarta"+$completo
			case ($text[$i] = 5)
				$text[$i] = "Quinta"+$completo
			case ($text[$i] = 6)
				$text[$i] = "Sexta"+$completo
			case ($text[$i] = 7)
				$text[$i] = "Sábado"
		endselect
	next
	$NDiaToTexto = Join($text, ",")
endfunction

function msg($title, $text, $style)
	$msg		= $system.MessageBox()
	$msg.Title	= $title
	$msg.Style	= $style
	$msg.Text	= $text
	$msg=$msg.Show()
endfunction

function CurrentDate()
	$CurrentDate = right("0"+@MdayNo, 2)+'/'+right("0"+@monthNo, 2)+'/'+@year
endfunction

function MaskTextBox($Text, $Mask)
	Dim
		$i, $x, $chr, $count
	$MaskTextBox = ''
	$Mask = StringToCharArray($Mask)
	for $i=1 to Len($Text)+1
		$chr	= Substr($Text, $i, 1)
		$count	= 0
		for each $x in $Mask
			if ($chr <> $x)
				$count=$count+1
			else
				Goto ':Fim'
			endif
		next
		::Fim
		if ($count < Ubound($Mask)+1)
			$MaskTextBox = $MaskTextBox + Cstr($chr)
		endif
	next
endfunction
